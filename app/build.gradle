import java.text.SimpleDateFormat

apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'


def buildTimestamp() {
    return (System.currentTimeMillis() / 1000).toInteger()
}

def buildTime() {
    def df = new SimpleDateFormat("hhmm") // you can change it
    df.setTimeZone(TimeZone.getTimeZone("UTC"))
    return df.format(new Date())
}

def buildDate() {
    def df = new SimpleDateFormat("yy.MMdd") // you can change it
    df.setTimeZone(TimeZone.getTimeZone("UTC"))
    return df.format(new Date())
}

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.github.simonpham.devtiles"
        minSdkVersion 24
        // Writing System Settings fails on higher versions
        //noinspection OldTargetApi
        targetSdkVersion 22
        versionCode 3
        versionName "1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        android.applicationVariants.all { variant ->
            variant.outputs.all {
                outputFileName = "${variant.name}-${variant.versionName}-${buildTimestamp()}.apk"
            }
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            versionNameSuffix ".${defaultConfig.versionCode}"
        }
        debug {
            versionNameSuffix ".${defaultConfig.versionCode}-d${buildDate()}-${buildTime()}"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'org.jetbrains.anko:anko-common:0.9'

    final supportLibVersion = '27.1.1'
    implementation "com.android.support:appcompat-v7:$supportLibVersion"
    implementation "com.android.support:design:$supportLibVersion"
    implementation "com.android.support:cardview-v7:$supportLibVersion"

    implementation 'com.android.support.constraint:constraint-layout:1.1.0'

    compileOnly files('libs/layoutlib.jar')
    implementation 'eu.chainfire:libsuperuser:1.0.0.201704021214'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
